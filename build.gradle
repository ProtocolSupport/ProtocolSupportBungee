 buildscript {
	dependencies {
		classpath 'org.jdom:jdom2:2.0.6'
		classpath 'org.ow2.asm:asm:5.1'
		classpath 'org.ow2.asm:asm-commons:5.1'
		classpath 'commons-io:commons-io:2.5'
		classpath 'org.apache.ant:ant:1.9.7'
		classpath 'org.codehaus.plexus:plexus-utils:3.0.24'
		classpath 'org.vafer:jdependency:1.1'
		classpath files('gradle/plugins/shadowPlugin.jar')
	}
	repositories {
		mavenCentral()
	}
}

plugins {
	id 'java'
}

apply plugin: com.github.jengelman.gradle.plugins.shadow.ShadowPlugin

tasks.withType(AbstractCompile) {
	classpath += configurations.shadow
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

group 'protocolsupport'
version '1.4.dev'

sourceCompatibility = 1.8


import java.text.MessageFormat

File dlDepsDirC = new File('dllibsc')
dlDepsDirC.mkdirs()

File dlDepsDirS = new File('dllibss')
dlDepsDirS.mkdirs()

def dlDepsC = [
	new Tuple(
		'https://ci.md-5.net/job/BungeeCord/1356/artifact/bootstrap/target/BungeeCord.jar',
		'bungeecord-2.jar'
	)
]

def dlDepsS = [
	new Tuple(
		'https://build.true-games.org/job/RakNetServer/29/artifact/target/RakNetServer.jar',
		'raknetserver-7.jar'
	),
	new Tuple(
		'https://build.true-games.org/job/UdpServerSocketChannel/11/artifact/target/UdpServerSocketChannel.jar',
		'udpserver-3.jar'
	)
]

void dlDeps(def dlDeps, File dlDepsDir) {
	def dlDepsNames = new HashSet<String>()
	dlDeps.each({
		String dlDepUrl = it[0]
		File dlDepFile = new File(dlDepsDir, it[1])
		dlDepsNames.add(dlDepFile.getName())
		if (!dlDepFile.exists()) {
			logger.lifecycle(MessageFormat.format("Downloading {0} from {1}", dlDepFile.getName(), dlDepUrl))
			ant.get(src: dlDepUrl, dest: dlDepFile)
		} else {
			logger.lifecycle(MessageFormat.format("Skipping download of {0} because it already exists", dlDepFile.getName()))
		}
	})
	dlDepsDir.listFiles().findAll({ !dlDepsNames.contains(it.getName()) }).each({
		logger.lifecycle(MessageFormat.format("Deleting old dllib {0}", it.getName()))
		it.delete()
	})
}

task updateLibs(type: DefaultTask) {doLast{
	dlDeps(dlDepsC, dlDepsDirC)
	dlDeps(dlDepsS, dlDepsDirS)
}}


sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
		resources {
			srcDirs = ['resources']
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	shadow fileTree(dir: dlDepsDirC, include: '*.jar')
	compile fileTree(dir: dlDepsDirS, include: '*.jar')
	compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
	compile group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '5.2'
	compile group: 'it.unimi.dsi', name: 'fastutil', version: '8.1.1'
}


shadowJar {
	doFirst {
		new File(destinationDir, archiveName).delete()
	}

	from sourceSets.main.java.srcDirs
	from 'LICENSE'

	minimizeJar = true

	destinationDir = file('target')
	archiveName = 'ProtocolSupportBungee.jar'

	exclude 'META-INF/**'
	relocate 'org.apache', 'protocolsupport.libs.org.apache'
	relocate 'com.nimbusds', 'protocolsupport.libs.com.nimbusds'
	relocate 'org.objectweb', 'protocolsupport.libs.org.objectweb'
	relocate 'net.minidev', 'protocolsupport.libs.net.minidev'
	relocate 'net.jcip', 'protocolsupport.libs.net.jcip'
	relocate 'raknetserver', 'protocolsupport.libs.raknetserver'
	relocate 'udpserversocketchannel', 'protocolsupport.libs.udpserversocketchannel'
	relocate 'it.unimi.dsi.fastutil', 'protocolsupport.libs.it.unimi.dsi.fastutil'
}


compileJava.dependsOn(clean)
compileJava.dependsOn(updateLibs)
jar.enabled = false
jar.finalizedBy(shadowJar)
